import com.axelor.gradle.tasks.GenerateAosChangelog
import com.axelor.gradle.tasks.CheckEOLCharsInCsv

buildscript {
  ext.repos = {
    mavenCentral() {
      content {
        excludeGroup 'com.axelor'
      }
    }
    maven {
      url 'https://plugins.gradle.org/m2/'
      content {
        excludeGroup 'com.axelor'
      }
    }
    maven { url 'https://repository.axelor.com/nexus/public/' }
  }
  ext.openPlatformVersion = '5.4.19'
  ext.appVersion = '6.5.3'
  repositories repos
  dependencies {
    classpath "com.axelor:axelor-gradle:${openPlatformVersion}"
  }
}

allprojects { repositories repos }

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: "com.axelor.app"

apply from: './gradle/style.gradle'

allprojects {

  configurations {
    runtimeClasspath.exclude group: "org.eclipse.birt.runtime.3_7_1", module: "org.apache.commons.codec"
  }

  apply plugin: 'idea'
  apply plugin: 'eclipse'

  group = "com.axelor"
  version = "${appVersion}"

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

}

subprojects {
  group = "com.axelor.apps"
}

axelor {
  title "Axelor ERP"
  description "Axelor Entreprise Application"
}

dependencies {
  ext.axelorVersion = '6.5.3'
  implementation "com.axelor.apps:axelor-account:${axelorVersion}"
  implementation "com.axelor.apps:axelor-admin:${axelorVersion}"
  implementation "com.axelor.apps:axelor-bank-payment:${axelorVersion}"
  implementation "com.axelor.apps:axelor-base:${axelorVersion}"
  implementation "com.axelor.apps:axelor-bpm:${axelorVersion}"
  implementation "com.axelor.apps:axelor-business-production:${axelorVersion}"
  implementation "com.axelor.apps:axelor-business-project:${axelorVersion}"
  implementation "com.axelor.apps:axelor-business-support:${axelorVersion}"
  implementation "com.axelor.apps:axelor-cash-management:${axelorVersion}"
  implementation "com.axelor.apps:axelor-client-portal:${axelorVersion}"
  implementation "com.axelor.apps:axelor-contract:${axelorVersion}"
  implementation "com.axelor.apps:axelor-crm:${axelorVersion}"
  implementation "com.axelor.apps:axelor-exception:${axelorVersion}"
  implementation "com.axelor.apps:axelor-fleet:${axelorVersion}"
  implementation "com.axelor.apps:axelor-gdpr:${axelorVersion}"
  implementation "com.axelor.apps:axelor-helpdesk:${axelorVersion}"
  implementation "com.axelor.apps:axelor-human-resource:${axelorVersion}"
  implementation "com.axelor.apps:axelor-maintenance:${axelorVersion}"
  implementation "com.axelor.apps:axelor-marketing:${axelorVersion}"
  implementation "com.axelor.apps:axelor-message:${axelorVersion}"
  implementation "com.axelor.apps:axelor-mobile:${axelorVersion}"
  implementation "com.axelor.apps:axelor-production:${axelorVersion}"
  implementation "com.axelor.apps:axelor-project:${axelorVersion}"
  implementation "com.axelor.apps:axelor-project-dms:${axelorVersion}"
  implementation "com.axelor.apps:axelor-purchase:${axelorVersion}"
  implementation "com.axelor.apps:axelor-quality:${axelorVersion}"
  implementation "com.axelor.apps:axelor-sale:${axelorVersion}"
  implementation "com.axelor.apps:axelor-stock:${axelorVersion}"
  implementation "com.axelor.apps:axelor-studio:${axelorVersion}"
  implementation "com.axelor.apps:axelor-supplier-management:${axelorVersion}"
  implementation "com.axelor.apps:axelor-supplier-portal:${axelorVersion}"
  implementation "com.axelor.apps:axelor-supplychain:${axelorVersion}"
  implementation "com.axelor.apps:axelor-talent:${axelorVersion}"
  implementation "com.axelor.apps:axelor-tool:${axelorVersion}"
}

task("dataImport", type: JavaExec) {
  main = "com.axelor.apps.erp.Main"
  classpath = sourceSets.main.runtimeClasspath
  if (System.getProperty("exec.args") != null) {
    args System.getProperty("exec.args").split()
  }
}

task generateChangeLog(type: GenerateAosChangelog) {
  group "Axelor application"
  description "Generate change logs from unreleased entries."
  files = fileTree(projectDir) {
    include '**/changelogs/**/*.yml'
    include '**/changelogs/**/*.yaml'
  }
}

task checkCsvEOL(type: CheckEOLCharsInCsv) {
  group "Axelor application"
  description "Generate change logs from unreleased entries."
  files = fileTree(projectDir) {
    exclude '**/build'
    exclude '**/bin'
    exclude '**/out'
    include '**/*.csv'
  }
}
